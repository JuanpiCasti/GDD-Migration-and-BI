USE GD1C2023;
GO

-- Eliminacion previa

IF OBJECT_ID('BLACKSUITS.BI_H_PEDIDO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_H_PEDIDO;
GO
IF OBJECT_ID('BLACKSUITS.BI_H_ENVIO_MENSAJERIA', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA;
GO
IF OBJECT_ID('BLACKSUITS.BI_H_RECLAMO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_H_RECLAMO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_RANGO_ETARIO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_RANGO_ETARIO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_LOCALIDAD', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_LOCALIDAD;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_PROVINCIA', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_PROVINCIA;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_RANGO_HORARIO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_RANGO_HORARIO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_MEDIO_PAGO_TIPO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_MEDIO_PAGO_TIPO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_TIEMPO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_TIEMPO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_PEDIDO_ESTADO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_PEDIDO_ESTADO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_DIA', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_DIA;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_TIPO_MOVILIDAD', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_TIPO_MOVILIDAD;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_CATEGORIA_LOCAL', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_CATEGORIA_LOCAL;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_TIPO_LOCAL', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_TIPO_LOCAL;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_LOCAL', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_LOCAL;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_RECLAMO_ESTADO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_RECLAMO_ESTADO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_RECLAMO_TIPO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_RECLAMO_TIPO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_PAQUETE_TIPO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_PAQUETE_TIPO;
GO
IF OBJECT_ID('BLACKSUITS.BI_D_ENVIO_MENSAJERIA_ESTADO', 'U') IS NOT NULL
    DROP TABLE BLACKSUITS.BI_D_ENVIO_MENSAJERIA_ESTADO;
GO

IF OBJECT_ID('BLACKSUITS.ObtenerRangoHorario', 'FN') IS NOT NULL
    DROP FUNCTION BLACKSUITS.ObtenerRangoHorario;
GO
IF OBJECT_ID('BLACKSUITS.ObtenerDiaSemana', 'FN') IS NOT NULL
    DROP FUNCTION BLACKSUITS.ObtenerDiaSemana;
GO
IF OBJECT_ID('BLACKSUITS.ObtenerRangoEtario', 'FN') IS NOT NULL
    DROP FUNCTION BLACKSUITS.ObtenerRangoEtario;
GO
IF OBJECT_ID('BLACKSUITS.LLENAR_D_RANGO_ETARIO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.LLENAR_D_RANGO_ETARIO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_PROVINCIA', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_PROVINCIA;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_LOCALIDAD', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_LOCALIDAD;
GO
IF OBJECT_ID('BLACKSUITS.LLENAR_D_RANGO_HORARIO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.LLENAR_D_RANGO_HORARIO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_MEDIO_PAGO_TIPO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_MEDIO_PAGO_TIPO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_TIEMPO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_TIEMPO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_PEDIDO_ESTADO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_PEDIDO_ESTADO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_DIA', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_DIA;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_TIPO_MOVILIDAD', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_TIPO_MOVILIDAD;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_LOCAL', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_LOCAL;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_TIPO_LOCAL', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_TIPO_LOCAL;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_CATEGORIA_LOCAL', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_CATEGORIA_LOCAL;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_RECLAMO_ESTADO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_RECLAMO_ESTADO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_RECLAMO_TIPO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_RECLAMO_TIPO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_PAQUETE_TIPO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_D_PAQUETE_TIPO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_H_PEDIDO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_H_PEDIDO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_H_ENVIO_MENSAJERIA', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_H_ENVIO_MENSAJERIA;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_H_RECLAMO', 'P') IS NOT NULL
    DROP PROCEDURE BLACKSUITS.MIGRAR_H_RECLAMO;
GO
IF OBJECT_ID('BLACKSUITS.MIGRAR_D_ENVIO_MENSAJERIA_ESTADO', 'P') IS NOT NULL
	DROP PROCEDURE BLACKSUITS.MIGRAR_D_ENVIO_MENSAJERIA_ESTADO;

IF OBJECT_ID('BLACKSUITS.dia_y_franja_mas_pedidos', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.dia_y_franja_mas_pedidos;
GO
IF OBJECT_ID('BLACKSUITS.monto_total_no_cobrado', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.monto_total_no_cobrado;
GO
IF OBJECT_ID('BLACKSUITS.valor_promedio_mensual_envios_localidad', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.valor_promedio_mensual_envios_localidad;
GO
IF OBJECT_ID('BLACKSUITS.desvio_promedio_tiempo_entrega', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.desvio_promedio_tiempo_entrega;
GO
IF OBJECT_ID('BLACKSUITS.monto_total_cupones_rango_etario', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.monto_total_cupones_rango_etario;
GO
IF OBJECT_ID('BLACKSUITS.promedio_calificacion_mensual_local', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.promedio_calificacion_mensual_local;
GO
IF OBJECT_ID('BLACKSUITS.porcentaje_pedidos_mensajeria_entregados', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.porcentaje_pedidos_mensajeria_entregados;
GO
IF OBJECT_ID('BLACKSUITS.promedio_valor_asegurado_tipo_paquete', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.promedio_valor_asegurado_tipo_paquete;
GO
IF OBJECT_ID('BLACKSUITS.cantidad_reclamos_local_dia_rango_horario', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.cantidad_reclamos_local_dia_rango_horario;
GO
IF OBJECT_ID('BLACKSUITS.tiempo_promedio_resolucion_reclamo', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.tiempo_promedio_resolucion_reclamo;
GO
IF OBJECT_ID('BLACKSUITS.monto_mensual_cupones_reclamos', 'V') IS NOT NULL
    DROP VIEW BLACKSUITS.monto_mensual_cupones_reclamos;
GO
-- Creacion tablas

CREATE TABLE BLACKSUITS.BI_D_RANGO_ETARIO (
    RANGO_ETARIO_ID int NOT NULL,
    EDAD_INICIO decimal(18,0),
    EDAD_FIN decimal(18,0)
)
GO

CREATE TABLE BLACKSUITS.BI_D_LOCALIDAD (
    LOCALIDAD_ID int NOT NULL,
    LOCALIDAD nvarchar(255),
    LOCALIDAD_PROVINCIA int NOT NULL
)
GO

CREATE TABLE BLACKSUITS.BI_D_PROVINCIA (
    PROVINCIA_ID int NOT NULL,
    PROVINCIA nvarchar(255),
)
GO

CREATE TABLE BLACKSUITS.BI_D_RANGO_HORARIO (
    RANGO_HORARIO_ID int NOT NULL,
    HORA_INICIO decimal(18,0),
    HORA_FIN decimal(18,0)
)
GO

CREATE TABLE BLACKSUITS.BI_D_MEDIO_PAGO_TIPO (
    MEDIO_PAGO_TIPO_ID int NOT NULL,
    MEDIO_PAGO_TIPO nvarchar(50)
)
GO

CREATE TABLE BLACKSUITS.BI_D_TIEMPO(
    ANIO int NOT NULL,
    MES int NOT NULL
)
GO

CREATE TABLE BLACKSUITS.BI_D_PEDIDO_ESTADO (
    ID_PEDIDO_ESTADO int NOT NULL,
    PEDIDO_ESTADO nvarchar(50)
)
GO

CREATE TABLE BLACKSUITS.BI_D_ENVIO_MENSAJERIA_ESTADO (
    ENVIO_MENSAJERIA_ESTADO_ID int NOT NULL,
    ENVIO_MENSAJERIA_ESTADO nvarchar(50)
)
GO

CREATE TABLE BLACKSUITS.BI_D_DIA (
    DIA_ID int NOT NULL,
    DIA_NOMBRE varchar(10)
)
GO

CREATE TABLE BLACKSUITS.BI_D_TIPO_MOVILIDAD(
    TIPO_MOVILIDAD_ID int NOT NULL,
    TIPO_MOVILIDAD nvarchar(50) 
)
GO


CREATE TABLE BLACKSUITS.BI_D_CATEGORIA_LOCAL(
    CATEGORIA_ID int NOT NULL,
    CATEGORIA nvarchar(50),
    CATEGORIA_TIPO_LOCAL_ID int
)
GO

CREATE TABLE BLACKSUITS.BI_D_TIPO_LOCAL(
    TIPO_LOCAL_ID int NOT NULL,
    LOCAL_TIPO nvarchar(50) 
)
GO

CREATE TABLE BLACKSUITS.BI_D_LOCAL(
    LOCAL_ID int NOT NULL,
    LOCAL_DIRECCION nvarchar(255),
    LOCAL_NOMBRE nvarchar(100),
    LOCAL_DESCRIPCION nvarchar(255) 
)
GO

CREATE TABLE BLACKSUITS.BI_D_RECLAMO_ESTADO(
    RECLAMO_ESTADO_ID int NOT NULL,
    RECLAMO_ESTADO nvarchar(50)
)
GO

CREATE TABLE BLACKSUITS.BI_D_RECLAMO_TIPO(
    RECLAMO_TIPO_ID int NOT NULL, 
    RECLAMO_TIPO nvarchar(50) 
)
GO

CREATE TABLE BLACKSUITS.BI_D_PAQUETE_TIPO(
    PAQUETE_TIPO_ID int NOT NULL,
    PAQUETE_TIPO nvarchar(50), 
    PAQUETE_TIPO_PRECIO decimal(18,2) 
)
GO

CREATE TABLE BLACKSUITS.BI_H_PEDIDO (
    RANGO_HORARIO_ID int NOT NULL,
    ANIO int NOT NULL,
    MES int NOT NULL,
    LOCAL_ID int NOT NULL,
    DIA_ID int NOT NULL,
    ID_PEDIDO_ESTADO int NOT NULL,
    RANGO_ETARIO_USUARIO_ID int NOT NULL,
    RANGO_ETARIO_REPARTIDOR_ID int NOT NULL,
    LOCALIDAD_ENVIO_ID int NOT NULL,
    CATEGORIA_ID int NOT NULL,
    LOCALIDAD_LOCAL_ID int NOT NULL,
    MEDIO_PAGO_TIPO_ID int NOT NULL,
    TIPO_MOVILIDAD_ID int NOT NULL,
    CANTIDAD_PEDIDOS int,
    TOTAL_PRODUCTOS decimal(18,2),
    TOTAL_ENVIOS decimal(18,2),
    TOTAL_TIEMPO_ENTREGA_REAL decimal(18,2),
    TOTAL_TIEMPO_ENTREGA_ESPERADO decimal(18,2),
    TOTAL_MONTO_CUPONES decimal(18,2),
    TOTAL_CALIFICACIONES decimal(18,0),
)
GO

CREATE TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA (
    RANGO_HORARIO_ID int NOT NULL,
    ANIO int NOT NULL,
    MES int NOT NULL,
    TIPO_MOVILIDAD_ID int NOT NULL,
    DIA_ID int NOT NULL,
    RANGO_ETARIO_ID int NOT NULL,
    LOCALIDAD_ID int NOT NULL,
    PAQUETE_TIPO_ID int NOT NULL,
    MEDIO_PAGO_TIPO_ID int NOT NULL,
    ENVIO_MENSAJERIA_ESTADO_ID int NOT NULL,
    CANTIDAD_ENVIOS int,
    TOTAL_TIEMPO_ENTREGA_REAL decimal(18,2),
    TOTAL_TIEMPO_ENTREGA_ESPERADO decimal(18,2),
    TOTAL_VALOR_ASEGURADO decimal(18,2)
)
GO

CREATE TABLE BLACKSUITS.BI_H_RECLAMO (
    ANIO int NOT NULL,
    MES int NOT NULL,
    RANGO_HORARIO_ID int NOT NULL,
    LOCAL_ID int NOT NULL,
    RANGO_ETARIO_ID int NOT NULL,
    RECLAMO_TIPO_ID int NOT NULL,
    DIA_ID int NOT NULL,
    RECLAMO_ESTADO_ID int NOT NULL,
    CANTIDAD_RECLAMOS int,
    TOTAL_TIEMPO_RESOLUCION decimal(18,2),
    TOTAL_CUPONES decimal(18,2)
)
GO

-- Creacion primary keys

ALTER TABLE BLACKSUITS.BI_D_RANGO_ETARIO ADD CONSTRAINT PK_BI_D_RANGO_ETARIO PRIMARY KEY (RANGO_ETARIO_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_LOCALIDAD ADD CONSTRAINT PK_BI_D_LOCALIDAD PRIMARY KEY (LOCALIDAD_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_PROVINCIA ADD CONSTRAINT PK_BI_D_PROVINCIA PRIMARY KEY (PROVINCIA_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_RANGO_HORARIO ADD CONSTRAINT PK_BI_D_RANGO_HORARIO PRIMARY KEY (RANGO_HORARIO_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_MEDIO_PAGO_TIPO ADD CONSTRAINT PK_BI_D_MEDIO_PAGO_TIPO PRIMARY KEY (MEDIO_PAGO_TIPO_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_TIEMPO ADD CONSTRAINT PK_BI_D_TIEMPO PRIMARY KEY (ANIO, MES);
GO

ALTER TABLE BLACKSUITS.BI_D_PEDIDO_ESTADO ADD CONSTRAINT PK_BI_D_PEDIDO_ESTADO PRIMARY KEY (ID_PEDIDO_ESTADO);
GO

ALTER TABLE BLACKSUITS.BI_D_ENVIO_MENSAJERIA_ESTADO ADD CONSTRAINT PK_BI_D_MENSAJERIA_ESTADO PRIMARY KEY (ENVIO_MENSAJERIA_ESTADO_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_DIA ADD CONSTRAINT PK_BI_D_DIA PRIMARY KEY (DIA_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_TIPO_MOVILIDAD ADD CONSTRAINT PK_BI_D_TIPO_MOVILIDAD PRIMARY KEY (TIPO_MOVILIDAD_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_CATEGORIA_LOCAL ADD CONSTRAINT PK_BI_D_CATEGORIA_LOCAL PRIMARY KEY (CATEGORIA_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_TIPO_LOCAL ADD CONSTRAINT PK_BI_D_TIPO_LOCAL PRIMARY KEY (TIPO_LOCAL_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_LOCAL ADD CONSTRAINT PK_BI_D_LOCAL PRIMARY KEY (LOCAL_ID);
GO
 
ALTER TABLE BLACKSUITS.BI_D_RECLAMO_ESTADO ADD CONSTRAINT PK_BI_D_RECLAMO_ESTADO PRIMARY KEY (RECLAMO_ESTADO_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_RECLAMO_TIPO ADD CONSTRAINT PK_BI_D_RECLAMO_TIPO PRIMARY KEY (RECLAMO_TIPO_ID);
GO


ALTER TABLE BLACKSUITS.BI_D_PAQUETE_TIPO ADD CONSTRAINT PK_BI_D_PAQUETE_TIPO PRIMARY KEY (PAQUETE_TIPO_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT PK_BI_H_PEDIDO  PRIMARY KEY (RANGO_HORARIO_ID,
                                                                                ANIO,
                                                                                MES,
                                                                                LOCAL_ID,
                                                                                DIA_ID,
                                                                                ID_PEDIDO_ESTADO,
                                                                                RANGO_ETARIO_USUARIO_ID,
                                                                                RANGO_ETARIO_REPARTIDOR_ID,
                                                                                LOCALIDAD_ENVIO_ID,
                                                                                CATEGORIA_ID,
                                                                                LOCALIDAD_LOCAL_ID,
                                                                                MEDIO_PAGO_TIPO_ID,
                                                                                TIPO_MOVILIDAD_ID
                                                                                );
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT PK_BI_H_ENVIO_MENSAJERIA PRIMARY KEY (RANGO_HORARIO_ID, 
                                                                                                    ANIO, 
                                                                                                    MES, 
                                                                                                    TIPO_MOVILIDAD_ID, 
                                                                                                    DIA_ID, 
                                                                                                    RANGO_ETARIO_ID,
                                                                                                    LOCALIDAD_ID,
                                                                                                    PAQUETE_TIPO_ID,
                                                                                                    MEDIO_PAGO_TIPO_ID,
                                                                                                    ENVIO_MENSAJERIA_ESTADO_ID
                                                                                                    );
GO

ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT PK_BI_H_RECLAMO PRIMARY KEY (ANIO, 
                                                                                MES, 
                                                                                RANGO_HORARIO_ID, 
                                                                                LOCAL_ID,
                                                                                RANGO_ETARIO_ID,
                                                                                RECLAMO_TIPO_ID,
                                                                                DIA_ID, 
                                                                                RECLAMO_ESTADO_ID
                                                                                );
GO

-- Creacion foreign keys

ALTER TABLE BLACKSUITS.BI_D_CATEGORIA_LOCAL ADD CONSTRAINT FK_D_LOCAL_CATEGORIA_TIPO_LOCAL FOREIGN KEY (CATEGORIA_TIPO_LOCAL_ID) REFERENCES BLACKSUITS.BI_D_TIPO_LOCAL(TIPO_LOCAL_ID);
GO

ALTER TABLE BLACKSUITS.BI_D_LOCALIDAD ADD CONSTRAINT FK_D_LOCALIDAD_PROVINCIA FOREIGN KEY (LOCALIDAD_PROVINCIA) REFERENCES BLACKSUITS.BI_D_PROVINCIA(PROVINCIA_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_RANGO_HORARIO FOREIGN KEY(RANGO_HORARIO_ID) REFERENCES BLACKSUITS.BI_D_RANGO_HORARIO(RANGO_HORARIO_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_TIEMPO FOREIGN KEY(ANIO, MES) REFERENCES BLACKSUITS.BI_D_TIEMPO(ANIO, MES);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_LOCAL FOREIGN KEY(LOCAL_ID) REFERENCES BLACKSUITS.BI_D_LOCAL(LOCAL_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_DIA FOREIGN KEY(DIA_ID) REFERENCES BLACKSUITS.BI_D_DIA(DIA_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_ESTADO FOREIGN KEY(ID_PEDIDO_ESTADO) REFERENCES BLACKSUITS.BI_D_PEDIDO_ESTADO(ID_PEDIDO_ESTADO);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_RANGO_ETARIO_USUARIO FOREIGN KEY(RANGO_ETARIO_USUARIO_ID) REFERENCES BLACKSUITS.BI_D_RANGO_ETARIO(RANGO_ETARIO_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_RANGO_ETARIO_REPARTIDOR FOREIGN KEY(RANGO_ETARIO_REPARTIDOR_ID) REFERENCES BLACKSUITS.BI_D_RANGO_ETARIO(RANGO_ETARIO_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_LOCALIDAD_ENVIO FOREIGN KEY(LOCALIDAD_ENVIO_ID) REFERENCES BLACKSUITS.BI_D_LOCALIDAD(LOCALIDAD_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_CATEGORIA FOREIGN KEY(CATEGORIA_ID) REFERENCES BLACKSUITS.BI_D_CATEGORIA_LOCAL(CATEGORIA_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_LOCALIDAD_LOCAL FOREIGN KEY(LOCALIDAD_LOCAL_ID) REFERENCES BLACKSUITS.BI_D_LOCALIDAD(LOCALIDAD_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_MEDIO_PAGO_TIPO FOREIGN KEY(MEDIO_PAGO_TIPO_ID) REFERENCES BLACKSUITS.BI_D_MEDIO_PAGO_TIPO(MEDIO_PAGO_TIPO_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_PEDIDO ADD CONSTRAINT FK_H_PEDIDO_TIPO_MOVILIDAD FOREIGN KEY(TIPO_MOVILIDAD_ID) REFERENCES BLACKSUITS.BI_D_TIPO_MOVILIDAD(TIPO_MOVILIDAD_ID);
GO


ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_RANGO_HORARIO FOREIGN KEY (RANGO_HORARIO_ID) REFERENCES BLACKSUITS.BI_D_RANGO_HORARIO( RANGO_HORARIO_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_TIEMPO FOREIGN KEY (ANIO, MES) REFERENCES BLACKSUITS.BI_D_TIEMPO(ANIO, MES);
GO
 
ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_TIPO_MOVILIDAD FOREIGN KEY (TIPO_MOVILIDAD_ID) REFERENCES BLACKSUITS.BI_D_TIPO_MOVILIDAD(TIPO_MOVILIDAD_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_DIA FOREIGN KEY (DIA_ID) REFERENCES BLACKSUITS.BI_D_DIA(DIA_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_RANGO_ETARIO  FOREIGN KEY (RANGO_ETARIO_ID) REFERENCES BLACKSUITS.BI_D_RANGO_ETARIO(RANGO_ETARIO_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_LOCALIDAD FOREIGN KEY (LOCALIDAD_ID) REFERENCES BLACKSUITS.BI_D_LOCALIDAD(LOCALIDAD_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_PAQUETE_TIPO FOREIGN KEY (PAQUETE_TIPO_ID) REFERENCES BLACKSUITS.BI_D_PAQUETE_TIPO(PAQUETE_TIPO_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_MEDIO_PAGO_TIPO FOREIGN KEY (MEDIO_PAGO_TIPO_ID) REFERENCES BLACKSUITS.BI_D_MEDIO_PAGO_TIPO(MEDIO_PAGO_TIPO_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_ENVIO_MENSAJERIA ADD CONSTRAINT FK_BI_H_ENVIO_MENSAJERIA_ESETADO FOREIGN KEY (ENVIO_MENSAJERIA_ESTADO_ID) REFERENCES BLACKSUITS.BI_D_ENVIO_MENSAJERIA_ESTADO(ENVIO_MENSAJERIA_ESTADO_ID);
GO

ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT FK_H_RECLAMO_TIEMPO FOREIGN KEY(ANIO, MES) REFERENCES BLACKSUITS.BI_D_TIEMPO(ANIO, MES);
GO
ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT FK_H_RECLAMO_RANGO_HORARIO FOREIGN KEY(RANGO_HORARIO_ID) REFERENCES BLACKSUITS.BI_D_RANGO_HORARIO(RANGO_HORARIO_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT FK_H_RECLAMO_LOCAL FOREIGN KEY(LOCAL_ID) REFERENCES BLACKSUITS.BI_D_LOCAL(LOCAL_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT FK_H_RECLAMO_RANGO_ETARIO FOREIGN KEY(RANGO_ETARIO_ID) REFERENCES BLACKSUITS.BI_D_RANGO_ETARIO(RANGO_ETARIO_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT FK_H_RECLAMO_RECLAMO_TIPO FOREIGN KEY(RECLAMO_TIPO_ID) REFERENCES BLACKSUITS.BI_D_RECLAMO_TIPO(RECLAMO_TIPO_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT FK_H_RECLAMO_DIA FOREIGN KEY(DIA_ID) REFERENCES BLACKSUITS.BI_D_DIA(DIA_ID);
GO
ALTER TABLE BLACKSUITS.BI_H_RECLAMO ADD CONSTRAINT FK_H_RECLAMO_RECLAMO_ESTADO FOREIGN KEY(RECLAMO_ESTADO_ID) REFERENCES BLACKSUITS.BI_D_RECLAMO_ESTADO(RECLAMO_ESTADO_ID);
GO

-- Funciones


CREATE FUNCTION BLACKSUITS.ObtenerRangoHorario(@fecha DATETIME)
RETURNS INT
AS
BEGIN
    DECLARE @rangoHorario INT

    SELECT @rangoHorario = RANGO_HORARIO_ID
    FROM (
        SELECT RANGO_HORARIO_ID, CAST(HORA_INICIO AS INT) AS HoraInicio, CAST(HORA_FIN AS INT) AS HoraFin
        FROM BLACKSUITS.BI_D_RANGO_HORARIO
    ) AS Rangos
    WHERE DATEPART(HOUR, @fecha) >= HoraInicio AND DATEPART(HOUR, @fecha) < HoraFin

    RETURN @rangoHorario
END
GO

CREATE FUNCTION BLACKSUITS.ObtenerDiaSemana(@fecha DATETIME)
RETURNS INT
AS
BEGIN
    DECLARE @diaSemana INT

    SELECT @diaSemana = CASE DATEPART(WEEKDAY, @fecha)
        WHEN 1 THEN 6   -- Domingo
        WHEN 2 THEN 7   -- Lunes
        WHEN 3 THEN 2   -- Martes
        WHEN 4 THEN 4   -- Miércoles
        WHEN 5 THEN 3   -- Jueves
        WHEN 6 THEN 1   -- Viernes
        WHEN 7 THEN 5   -- Sábado
    END

    RETURN @diaSemana
END
GO

CREATE FUNCTION BLACKSUITS.ObtenerRangoEtario(@fechaNacimiento DATE)
RETURNS INT
AS
BEGIN
    DECLARE @rangoEtario INT

    SELECT @rangoEtario = RANGO_ETARIO_ID
    FROM (
        SELECT RANGO_ETARIO_ID, CAST(EDAD_INICIO AS INT) AS EdadInicio, CAST(EDAD_FIN AS INT) AS EdadFin
        FROM BLACKSUITS.BI_D_RANGO_ETARIO
    ) AS Rangos
    WHERE DATEDIFF(YEAR, @fechaNacimiento, GETDATE()) >= EdadInicio AND DATEDIFF(YEAR, @fechaNacimiento, GETDATE()) < EdadFin

    RETURN @rangoEtario
END
GO

-- Migraciones

CREATE PROCEDURE BLACKSUITS.LLENAR_D_RANGO_ETARIO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_RANGO_ETARIO VALUES
    (1,0,25),
    (2,25,35),
    (3,35,55),
    (4,55,100)
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_D_PROVINCIA
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_PROVINCIA(PROVINCIA_ID, PROVINCIA)
    SELECT PROVINCIA_ID, PROVINCIA FROM BLACKSUITS.PROVINCIA
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_D_LOCALIDAD
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_LOCALIDAD(LOCALIDAD_ID, LOCALIDAD, LOCALIDAD_PROVINCIA)
    SELECT LOCALIDAD_ID, LOCALIDAD, LOCALIDAD_PROVINCIA FROM BLACKSUITS.LOCALIDAD
END
GO

CREATE PROCEDURE BLACKSUITS.LLENAR_D_RANGO_HORARIO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_RANGO_HORARIO VALUES
    (1, 8, 10),
    (2, 10,12),
    (3, 12, 14),
    (4, 14, 16),
    (5, 16, 18),
    (6, 18, 20),
    (7, 20, 22),
    (8, 22, 24);
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_D_MEDIO_PAGO_TIPO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_MEDIO_PAGO_TIPO(MEDIO_PAGO_TIPO_ID, MEDIO_PAGO_TIPO)
    SELECT MEDIO_PAGO_TIPO_ID, MEDIO_PAGO_TIPO FROM BLACKSUITS.MEDIO_PAGO_TIPO
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_D_TIEMPO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_TIEMPO (ANIO, MES)
    VALUES
        (2022, 1), (2022, 2), (2022, 3), (2022, 4), (2022, 5), (2022, 6),
        (2022, 7), (2022, 8), (2022, 9), (2022, 10), (2022, 11), (2022, 12),
        (2023, 1), (2023, 2), (2023, 3), (2023, 4), (2023, 5), (2023, 6),
        (2023, 7), (2023, 8), (2023, 9), (2023, 10), (2023, 11), (2023, 12),
        (2024, 1), (2024, 2), (2024, 3), (2024, 4), (2024, 5), (2024, 6),
        (2024, 7), (2024, 8), (2024, 9), (2024, 10), (2024, 11), (2024, 12)
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_D_PEDIDO_ESTADO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_PEDIDO_ESTADO(ID_PEDIDO_ESTADO, PEDIDO_ESTADO)
    SELECT ID_PEDIDO_ESTADO, PEDIDO_ESTADO FROM BLACKSUITS.PEDIDO_ESTADO
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_D_ENVIO_MENSAJERIA_ESTADO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_ENVIO_MENSAJERIA_ESTADO(ENVIO_MENSAJERIA_ESTADO_ID, ENVIO_MENSAJERIA_ESTADO)
    SELECT ENVIO_MENSAJERIA_ESTADO_ID, ENVIO_MENSAJERIA_ESTADO FROM BLACKSUITS.ENVIO_MENSAJERIA_ESTADO
END
GO


CREATE PROCEDURE BLACKSUITS.MIGRAR_D_DIA
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_D_DIA(DIA_ID, DIA_NOMBRE)
    SELECT HORARIO_LOCAL_DIA_ID, HORARIO_LOCAL_DIA FROM BLACKSUITS.HORARIO_LOCAL_DIA
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_D_TIPO_MOVILIDAD
AS
BEGIN
	INSERT INTO BLACKSUITS.BI_D_TIPO_MOVILIDAD(TIPO_MOVILIDAD_ID, TIPO_MOVILIDAD)
	SELECT TIPO_MOVILIDAD_ID, TIPO_MOVILIDAD
	FROM BLACKSUITS.TIPO_MOVILIDAD
END
GO


CREATE PROCEDURE BLACKSUITS.MIGRAR_D_LOCAL
AS
BEGIN
	INSERT INTO BLACKSUITS.BI_D_LOCAL(LOCAL_ID, LOCAL_DIRECCION, LOCAL_NOMBRE, LOCAL_DESCRIPCION)
	SELECT LOCAL_ID, LOCAL_DIRECCION, LOCAL_NOMBRE, LOCAL_DESCRIPCION
	FROM BLACKSUITS.LOCAL
END
GO


CREATE PROCEDURE BLACKSUITS.MIGRAR_D_TIPO_LOCAL
AS
BEGIN
	INSERT INTO BLACKSUITS.BI_D_TIPO_LOCAL(TIPO_LOCAL_ID, LOCAL_TIPO)
	SELECT TIPO_LOCAL_ID, LOCAL_TIPO
	FROM BLACKSUITS.TIPO_LOCAL
END
GO


CREATE PROCEDURE BLACKSUITS.MIGRAR_D_CATEGORIA_LOCAL
AS
BEGIN
	INSERT INTO BLACKSUITS.BI_D_CATEGORIA_LOCAL(CATEGORIA_ID, CATEGORIA, CATEGORIA_TIPO_LOCAL_ID)
	SELECT CATEGORIA_ID, CATEGORIA, CATEGORIA_TIPO_LOCAL_ID
	FROM BLACKSUITS.CATEGORIA
END
GO


CREATE PROCEDURE BLACKSUITS.MIGRAR_D_RECLAMO_ESTADO
AS
BEGIN
	INSERT INTO BLACKSUITS.BI_D_RECLAMO_ESTADO(RECLAMO_ESTADO_ID, RECLAMO_ESTADO)
	SELECT RECLAMO_ESTADO_ID, RECLAMO_ESTADO
	FROM BLACKSUITS.RECLAMO_ESTADO
END
GO


CREATE PROCEDURE BLACKSUITS.MIGRAR_D_RECLAMO_TIPO
AS
BEGIN
	INSERT INTO BLACKSUITS.BI_D_RECLAMO_TIPO(RECLAMO_TIPO_ID, RECLAMO_TIPO)
	SELECT RECLAMO_TIPO_ID, RECLAMO_TIPO
	FROM BLACKSUITS.RECLAMO_TIPO
END
GO


CREATE PROCEDURE BLACKSUITS.MIGRAR_D_PAQUETE_TIPO
AS
BEGIN
	INSERT INTO BLACKSUITS.BI_D_PAQUETE_TIPO(PAQUETE_TIPO_ID, PAQUETE_TIPO, PAQUETE_TIPO_PRECIO)
	SELECT PAQUETE_TIPO_ID, PAQUETE_TIPO, PAQUETE_TIPO_PRECIO
	FROM BLACKSUITS.PAQUETE_TIPO
END
GO



CREATE PROCEDURE BLACKSUITS.MIGRAR_H_PEDIDO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_H_PEDIDO (RANGO_HORARIO_ID,
                                         ANIO,
                                         MES,
                                         LOCAL_ID,
                                         DIA_ID,
                                         ID_PEDIDO_ESTADO,
                                         RANGO_ETARIO_USUARIO_ID,
                                         RANGO_ETARIO_REPARTIDOR_ID,
                                         LOCALIDAD_ENVIO_ID,
                                         CATEGORIA_ID,
                                         LOCALIDAD_LOCAL_ID,
                                         MEDIO_PAGO_TIPO_ID,
                                         TIPO_MOVILIDAD_ID,
                                         CANTIDAD_PEDIDOS,
                                         TOTAL_PRODUCTOS,
                                         TOTAL_ENVIOS,
                                         TOTAL_TIEMPO_ENTREGA_REAL,
                                         TOTAL_TIEMPO_ENTREGA_ESPERADO,
                                         TOTAL_MONTO_CUPONES,
                                         TOTAL_CALIFICACIONES)
    SELECT BLACKSUITS.ObtenerRangoHorario(p.PEDIDO_FECHA),
        YEAR(p.PEDIDO_FECHA),
        MONTH(p.PEDIDO_FECHA),
        l.LOCAL_ID,
        BLACKSUITS.ObtenerDiaSemana(p.PEDIDO_FECHA),
        p.PEDIDO_ESTADO,
        BLACKSUITS.ObtenerRangoEtario(u.USUARIO_FECHA_NAC),
        BLACKSUITS.ObtenerRangoEtario(r.REPARTIDOR_FECHA_NAC),
        d.DIRECCION_USUARIO_LOCALIDAD,
        l.LOCAL_CATEGORIA,
        l.LOCAL_LOCALIDAD,
        m.MEDIO_PAGO_TIPO,
        r.REPARTIDOR_TIPO_MOVILIDAD,
        COUNT(*),
        SUM(p.PEDIDO_TOTAL_PRODUCTOS),
        SUM(e.ENVIO_PRECIO),
        SUM(CAST(DATEDIFF(minute, p.PEDIDO_FECHA, p.PEDIDO_FECHA_ENTREGA) AS DECIMAL(18, 2))),
        SUM(p.PEDIDO_TIEMPO_ESTIMADO_ENTREGA),
        SUM(p.PEDIDO_TOTAL_CUPONES),
        SUM(p.PEDIDO_CALIFICACION)
        
    FROM BLACKSUITS.PEDIDO AS p
    JOIN BLACKSUITS.LOCAL AS l ON p.PEDIDO_LOCAL = l.LOCAL_ID
    JOIN BLACKSUITS.USUARIO AS u ON p.PEDIDO_USUARIO = u.USUARIO_MAIL
    JOIN BLACKSUITS.ENVIO AS e ON p.PEDIDO_ENVIO = e.ENVIO_ID
    JOIN BLACKSUITS.REPARTIDOR AS r ON e.ENVIO_REPARTIDOR = r.REPARTIDOR_ID
    JOIN BLACKSUITS.DIRECCION_USUARIO AS d ON e.ENVIO_DIRECCION_NOMBRE = d.DIRECCION_USUARIO_NOMBRE AND e.ENVIO_DIRECCION_USUARIO = d.DIRECCION_USUARIO 
    JOIN BLACKSUITS.MEDIO_PAGO AS m ON p.PEDIDO_MEDIO_PAGO = m.MEDIO_PAGO_NRO_TARJETA
    GROUP BY
		BLACKSUITS.ObtenerRangoHorario(p.PEDIDO_FECHA),
        YEAR(p.PEDIDO_FECHA),
        MONTH(p.PEDIDO_FECHA),
        l.LOCAL_ID,
        BLACKSUITS.ObtenerDiaSemana(p.PEDIDO_FECHA),
        p.PEDIDO_ESTADO,
        BLACKSUITS.ObtenerRangoEtario(u.USUARIO_FECHA_NAC),
        BLACKSUITS.ObtenerRangoEtario(r.REPARTIDOR_FECHA_NAC),
        d.DIRECCION_USUARIO_LOCALIDAD,
        l.LOCAL_CATEGORIA,
        l.LOCAL_LOCALIDAD,
        m.MEDIO_PAGO_TIPO,
        r.REPARTIDOR_TIPO_MOVILIDAD
END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_H_ENVIO_MENSAJERIA
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_H_ENVIO_MENSAJERIA(
        RANGO_HORARIO_ID,
        ANIO,
        MES,
        TIPO_MOVILIDAD_ID,
        DIA_ID,
        RANGO_ETARIO_ID,
        LOCALIDAD_ID,
        PAQUETE_TIPO_ID,
        MEDIO_PAGO_TIPO_ID,
        ENVIO_MENSAJERIA_ESTADO_ID,
        CANTIDAD_ENVIOS,
        TOTAL_TIEMPO_ENTREGA_REAL,
        TOTAL_TIEMPO_ENTREGA_ESPERADO,
        TOTAL_VALOR_ASEGURADO
    ) 
    SELECT BLACKSUITS.ObtenerRangoHorario(m.ENVIO_MENSAJERIA_FECHA),
        YEAR(m.ENVIO_MENSAJERIA_FECHA),
        MONTH(m.ENVIO_MENSAJERIA_FECHA),
        r.REPARTIDOR_TIPO_MOVILIDAD,
        BLACKSUITS.ObtenerDiaSemana(m.ENVIO_MENSAJERIA_FECHA),
        BLACKSUITS.ObtenerRangoEtario(r.REPARTIDOR_FECHA_NAC),
        m.ENVIO_MENSAJERIA_LOCALIDAD,
        p.PAQUETE_TIPO_ID,
        mp.MEDIO_PAGO_TIPO,
        m.ENVIO_MENSAJERIA_ESTADO,
        COUNT(*),
        SUM(CAST(DATEDIFF(minute, m.ENVIO_MENSAJERIA_FECHA, m.ENVIO_MENSAJERIA_FECHA_ENTREGA) AS DECIMAL(18, 2))),
        SUM(m.ENVIO_MENSAJERIA_TIEMPO_ESTIMADO),
        SUM(m.ENVIO_MENSAJERIA_VALOR_ASEGURADO)
    FROM BLACKSUITS.MENSAJERIA AS m
    JOIN BLACKSUITS.REPARTIDOR AS r ON r.REPARTIDOR_ID = m.ENVIO_MENSAJERIA_REPARTIDOR
    JOIN BLACKSUITS.PAQUETE AS p ON p.PAQUETE_ID = m.ENVIO_MENSAJERIA_PAQUETE
    JOIN BLACKSUITS.MEDIO_PAGO AS mp ON mp.MEDIO_PAGO_NRO_TARJETA = m.ENVIO_MENSAJERIA_MEDIO_PAGO
    GROUP BY 
        BLACKSUITS.ObtenerRangoHorario(m.ENVIO_MENSAJERIA_FECHA),
        YEAR(m.ENVIO_MENSAJERIA_FECHA),
        MONTH(m.ENVIO_MENSAJERIA_FECHA),
        r.REPARTIDOR_TIPO_MOVILIDAD,
        BLACKSUITS.ObtenerDiaSemana(m.ENVIO_MENSAJERIA_FECHA),
        BLACKSUITS.ObtenerRangoEtario(r.REPARTIDOR_FECHA_NAC),
        m.ENVIO_MENSAJERIA_LOCALIDAD,
        p.PAQUETE_TIPO_ID,
        mp.MEDIO_PAGO_TIPO,
		m.ENVIO_MENSAJERIA_ESTADO

END
GO

CREATE PROCEDURE BLACKSUITS.MIGRAR_H_RECLAMO
AS
BEGIN
    INSERT INTO BLACKSUITS.BI_H_RECLAMO(
        ANIO,
        MES,
        RANGO_HORARIO_ID,
        LOCAL_ID,
        RANGO_ETARIO_ID,
        RECLAMO_TIPO_ID,
        DIA_ID,
        RECLAMO_ESTADO_ID,
        CANTIDAD_RECLAMOS,
        TOTAL_TIEMPO_RESOLUCION,
        TOTAL_CUPONES
    )
    SELECT 
    YEAR(r.RECLAMO_FECHA),
    MONTH(r.RECLAMO_FECHA),
    BLACKSUITS.ObtenerRangoHorario(r.RECLAMO_FECHA),
    p.PEDIDO_LOCAL,
    BLACKSUITS.ObtenerRangoEtario(o.OPERADOR_RECLAMO_FECHA_NAC),
    r.RECLAMO_TIPO,
    BLACKSUITS.ObtenerDiaSemana(r.RECLAMO_FECHA),
    r.RECLAMO_ESTADO,
    COUNT(*),
    SUM(CAST(DATEDIFF(minute, r.RECLAMO_FECHA, r.RECLAMO_FECHA_SOLUCION) AS DECIMAL(18, 2))),
    SUM(cd.CUPON_MONTO)
    FROM BLACKSUITS.RECLAMO AS r
    JOIN BLACKSUITS.OPERADOR AS o ON o.OPERADOR_RECLAMO_ID = r.RECLAMO_OPERADOR_RECLAMO_ID
	JOIN BLACKSUITS.PEDIDO AS p ON p.PEDIDO_NRO = r.RECLAMO_PEDIDO_NRO
    LEFT JOIN BLACKSUITS.CUPON_X_RECLAMO as c ON c.RECLAMO_NRO = r.RECLAMO_NRO -- Es left join para que no se pierdan los reclamos que no tienen cupon
    LEFT JOIN BLACKSUITS.CUPON_DESCUENTO as cd ON c.CUPON_NRO = cd.CUPON_NRO AND cd.CUPON_USUARIO = c.CUPON_USUARIO 
    GROUP BY
    YEAR(r.RECLAMO_FECHA),
    MONTH(r.RECLAMO_FECHA),
    BLACKSUITS.ObtenerRangoHorario(r.RECLAMO_FECHA),
    p.PEDIDO_LOCAL,
    BLACKSUITS.ObtenerRangoEtario(o.OPERADOR_RECLAMO_FECHA_NAC),
    r.RECLAMO_TIPO,
    BLACKSUITS.ObtenerDiaSemana(r.RECLAMO_FECHA),
    r.RECLAMO_ESTADO
END
GO

EXEC BLACKSUITS.LLENAR_D_RANGO_ETARIO;
GO
EXEC BLACKSUITS.MIGRAR_D_PROVINCIA;
GO
EXEC BLACKSUITS.MIGRAR_D_LOCALIDAD;
GO
EXEC BLACKSUITS.LLENAR_D_RANGO_HORARIO;
GO
EXEC BLACKSUITS.MIGRAR_D_MEDIO_PAGO_TIPO;
GO
EXEC BLACKSUITS.MIGRAR_D_TIEMPO;
GO
EXEC BLACKSUITS.MIGRAR_D_PEDIDO_ESTADO;
GO
EXEC BLACKSUITS.MIGRAR_D_ENVIO_MENSAJERIA_ESTADO;
GO
EXEC BLACKSUITS.MIGRAR_D_DIA;
GO
EXEC BLACKSUITS.MIGRAR_D_TIPO_MOVILIDAD;
GO
EXEC BLACKSUITS.MIGRAR_D_LOCAL;
GO
EXEC BLACKSUITS.MIGRAR_D_TIPO_LOCAL;
GO
EXEC BLACKSUITS.MIGRAR_D_CATEGORIA_LOCAL;
GO
EXEC BLACKSUITS.MIGRAR_D_RECLAMO_ESTADO;
GO
EXEC BLACKSUITS.MIGRAR_D_RECLAMO_TIPO;
GO
EXEC BLACKSUITS.MIGRAR_D_PAQUETE_TIPO;
GO
EXEC BLACKSUITS.MIGRAR_H_PEDIDO;
GO
EXEC BLACKSUITS.MIGRAR_H_ENVIO_MENSAJERIA;
GO
EXEC BLACKSUITS.MIGRAR_H_RECLAMO;
GO


-- Vista 1

/*
 Dia de la semana y franja horaria con mayor cantidad de pedidos según 
 la localidad y categoría del local, para cada mes de cada año.
*/

CREATE VIEW BLACKSUITS.dia_y_franja_mas_pedidos
AS
    SELECT p.LOCALIDAD_LOCAL_ID, p.CATEGORIA_ID, p.MES, p.ANIO, 
	(SELECT TOP 1 d.DIA_NOMBRE
	FROM BLACKSUITS.BI_H_PEDIDO AS p1
	JOIN BLACKSUITS.BI_D_DIA AS d ON d.DIA_ID = p1.DIA_ID
	WHERE p1.LOCALIDAD_LOCAL_ID = p.LOCALIDAD_LOCAL_ID AND p1.CATEGORIA_ID = p.CATEGORIA_ID AND p1.ANIO = p.ANIO AND p1.MES = p.MES
	ORDER BY SUM(p.CANTIDAD_PEDIDOS) DESC
	) dia_semana_mas_pedidos,
	(SELECT TOP 1 r.RANGO_HORARIO_ID
	FROM BLACKSUITS.BI_H_PEDIDO AS p1
	JOIN BLACKSUITS.BI_D_RANGO_HORARIO AS r ON r.RANGO_HORARIO_ID = p1.RANGO_HORARIO_ID
	WHERE p1.LOCALIDAD_LOCAL_ID = p.LOCALIDAD_LOCAL_ID AND p1.CATEGORIA_ID = p.CATEGORIA_ID AND p1.ANIO = p.ANIO AND p1.MES = p.MES
	ORDER BY SUM(p.CANTIDAD_PEDIDOS) DESC
	) franja_horaria_mas_pedidos
    FROM BLACKSUITS.BI_H_PEDIDO AS p
    GROUP BY p.LOCALIDAD_LOCAL_ID, p.CATEGORIA_ID, p.MES, p.ANIO
	HAVING (
		SELECT TOP 1 SUM(p.CANTIDAD_PEDIDOS)
		FROM BLACKSUITS.BI_H_PEDIDO AS p1
		WHERE p1.LOCALIDAD_LOCAL_ID = p.LOCALIDAD_LOCAL_ID AND p1.CATEGORIA_ID = p.CATEGORIA_ID AND p1.ANIO = p.ANIO AND p1.MES = p.MES
		ORDER BY SUM(p.CANTIDAD_PEDIDOS) DESC
	) != 0
GO

/*
Monto total no cobrado por cada local en funcion de los pedidos
cancelados segun el dia de la semana y la franja horaria (cuentan como
pedidos cancelados tanto los que cancela el usuario como el local).
*/

CREATE VIEW BLACKSUITS.monto_total_no_cobrado
AS
	SELECT p.LOCAL_ID, d.DIA_NOMBRE, r.HORA_INICIO hora_inicio_rango_horario, r.HORA_FIN hora_fin_rango_horario, SUM(p.TOTAL_PRODUCTOS) total_no_cobrado
	FROM BLACKSUITS.BI_H_PEDIDO AS p
	JOIN BLACKSUITS.BI_D_DIA AS d ON d.DIA_ID = p.DIA_ID
	JOIN BLACKSUITS.BI_D_RANGO_HORARIO AS r ON r.RANGO_HORARIO_ID = p.RANGO_HORARIO_ID
	WHERE p.ID_PEDIDO_ESTADO = 2
	GROUP BY p.LOCAL_ID, p.DIA_ID, p.RANGO_HORARIO_ID, d.DIA_NOMBRE, r.HORA_INICIO, r.HORA_FIN
GO

/*
Valor promedio mensual que tienen los envios de pedidos en cada
localidad.
*/

CREATE VIEW BLACKSUITS.valor_promedio_mensual_envios_localidad
AS
	SELECT p.LOCALIDAD_ENVIO_ID, p.MES , p.ANIO, sum(p.TOTAL_ENVIOS)/sum(p.CANTIDAD_PEDIDOS) valor_promedio_envios
	FROM BLACKSUITS.BI_H_PEDIDO AS p
	GROUP BY p.LOCALIDAD_ENVIO_ID, p.MES, p.ANIO
GO

/*
Desv�o promedio en tiempo de entrega seg�n el tipo de movilidad, el d�a
de la semana y la franja horaria.
El desv�o debe calcularse en minutos y representa la diferencia entre la
fecha/hora en que se realiz� el pedido y la fecha/hora que se entreg� en
comparaci�n con los minutos de tiempo estimados.
Este indicador debe tener en cuenta todos los env�os, es decir, sumar tanto
los env�os de pedidos como los de mensajer�a.
*/

CREATE VIEW BLACKSUITS.desvio_promedio_tiempo_entrega
AS
    SELECT ((SUM(tiempo_real) - SUM(tiempo_esperado)) / SUM(cantidad)) desvio_promedio, sub.TIPO_MOVILIDAD_ID, sub.DIA_ID, sub.RANGO_HORARIO_ID
	FROM
	(SELECT SUM(TOTAL_TIEMPO_ENTREGA_REAL) tiempo_real, SUM(TOTAL_TIEMPO_ENTREGA_ESPERADO) tiempo_esperado, SUM(CANTIDAD_PEDIDOS) cantidad
    ,TIPO_MOVILIDAD_ID, DIA_ID, RANGO_HORARIO_ID
	FROM BLACKSUITS.BI_H_PEDIDO
	GROUP BY TIPO_MOVILIDAD_ID, DIA_ID, RANGO_HORARIO_ID
	UNION ALL
	SELECT SUM(TOTAL_TIEMPO_ENTREGA_REAL) tiempo_real, SUM(TOTAL_TIEMPO_ENTREGA_ESPERADO) tiempo_esperado, SUM(CANTIDAD_ENVIOS) cantidad,
	TIPO_MOVILIDAD_ID, DIA_ID, RANGO_HORARIO_ID
    FROM BLACKSUITS.BI_H_ENVIO_MENSAJERIA
	GROUP BY TIPO_MOVILIDAD_ID, DIA_ID, RANGO_HORARIO_ID) AS sub
    GROUP BY sub.TIPO_MOVILIDAD_ID, sub.DIA_ID, sub.RANGO_HORARIO_ID
GO

/*
Monto total de los cupones utilizados por mes en función del rango etario
de los usuarios.
*/

CREATE VIEW BLACKSUITS.monto_total_cupones_rango_etario
AS

    SELECT p.RANGO_ETARIO_USUARIO_ID, p.MES,p.ANIO, SUM(p.TOTAL_MONTO_CUPONES) monto_total_cupones
    FROM BLACKSUITS.BI_H_PEDIDO AS p
    GROUP BY p.RANGO_ETARIO_USUARIO_ID, p.MES, p.ANIO

GO

/*
Promedio de calificación mensual por local.
*/

CREATE VIEW BLACKSUITS.promedio_calificacion_mensual_local
AS
    SELECT p.LOCAL_ID, p.MES, p.ANIO,SUM(p.TOTAL_CALIFICACIONES) / SUM(p.CANTIDAD_PEDIDOS) promedio_calificacion
    FROM BLACKSUITS.BI_H_PEDIDO AS p
    GROUP BY p.LOCAL_ID, p.MES, p.ANIO
GO

--/*
--Porcentaje de pedidos y mensajería entregados mensualmente según el
--rango etario de los repartidores y la localidad.
--Este indicador se debe tener en cuenta y sumar tanto los envíos de pedidos
--como los de mensajería.
--El porcentaje se calcula en función del total general de pedidos y envíos
--mensuales entregados.
--*/

CREATE VIEW BLACKSUITS.porcentaje_pedidos_mensajeria_entregados
AS
	SELECT ROUND((SUM(cant_entregados) / SUM(cant_total))* 100, 2) porcentaje_entregados, rango_etario, localidad
	FROM(
	SELECT CAST(SUM(p.CANTIDAD_PEDIDOS) AS FLOAT) cant_entregados, (
		SELECT CAST(SUM(p1.CANTIDAD_PEDIDOS) AS FLOAT)
		FROM BLACKSUITS.BI_H_PEDIDO AS p1
		WHERE p.RANGO_ETARIO_REPARTIDOR_ID = p1.RANGO_ETARIO_REPARTIDOR_ID
			AND p.LOCALIDAD_LOCAL_ID = p1.LOCALIDAD_LOCAL_ID
	) cant_total, RANGO_ETARIO_REPARTIDOR_ID rango_etario, LOCALIDAD_LOCAL_ID localidad
	FROM BLACKSUITS.BI_H_PEDIDO AS p
	JOIN BLACKSUITS.BI_D_PEDIDO_ESTADO AS e ON e.ID_PEDIDO_ESTADO = p.ID_PEDIDO_ESTADO
	WHERE e.PEDIDO_ESTADO = 'Estado Mensajeria Entregado'
	GROUP BY RANGO_ETARIO_REPARTIDOR_ID, LOCALIDAD_LOCAL_ID
	UNION ALL
	SELECT 
	CAST(SUM(e.CANTIDAD_ENVIOS) AS FLOAT) cant_entregados, (
		SELECT CAST(SUM(e1.CANTIDAD_ENVIOS) AS FLOAT)
		FROM BLACKSUITS.BI_H_ENVIO_MENSAJERIA AS e1
		WHERE e.RANGO_ETARIO_ID = e1.RANGO_ETARIO_ID
			AND e.LOCALIDAD_ID = e1.LOCALIDAD_ID
	) cant_total, e.RANGO_ETARIO_ID rango_etario, e.LOCALIDAD_ID localidad
	FROM BLACKSUITS.BI_H_ENVIO_MENSAJERIA AS e
	JOIN BLACKSUITS.BI_D_ENVIO_MENSAJERIA_ESTADO AS s ON e.ENVIO_MENSAJERIA_ESTADO_ID = s.ENVIO_MENSAJERIA_ESTADO_ID
	WHERE s.ENVIO_MENSAJERIA_ESTADO = 'Estado Mensajeria Entregado'
	GROUP BY e.RANGO_ETARIO_ID, e.LOCALIDAD_ID
	) AS sub
	GROUP BY sub.rango_etario, sub.localidad
GO

/*
Promedio mensual del valor asegurado (valor declarado por el usuario) de
los paquetes enviados a través del servicio de mensajería en función del
tipo de paquete.*/
CREATE VIEW BLACKSUITS.promedio_valor_asegurado_tipo_paquete
AS
    SELECT SUM(e.TOTAL_VALOR_ASEGURADO) / SUM(e.CANTIDAD_ENVIOS) promedio_valor_asegurado, e.PAQUETE_TIPO_ID, e.MES, e.ANIO
    FROM BLACKSUITS.BI_H_ENVIO_MENSAJERIA AS e
    GROUP BY e.PAQUETE_TIPO_ID, e.MES, e.ANIO
GO

/*
Cantidad de reclamos mensuales recibidos por cada local en función del
día de la semana y rango horario.*/
CREATE VIEW BLACKSUITS.cantidad_reclamos_local_dia_rango_horario
AS
    SELECT SUM(CANTIDAD_RECLAMOS) cantidad_reclamos, r.LOCAL_ID, r.DIA_ID, r.RANGO_HORARIO_ID, r.MES, r.ANIO
    FROM BLACKSUITS.BI_H_RECLAMO AS r
    GROUP BY r.LOCAL_ID, r.DIA_ID, r.RANGO_HORARIO_ID, r.MES, r.ANIO
GO

/*
Tiempo promedio de resolución de reclamos mensual según cada tipo de
reclamo y rango etario de los operadores.
El tiempo de resolución debe calcularse en minutos y representa la
diferencia entre la fecha/hora en que se realizó el reclamo y la fecha/hora
que se resolvió.
*/


CREATE VIEW BLACKSUITS.tiempo_promedio_resolucion_reclamo
AS
   SELECT SUM(r.TOTAL_TIEMPO_RESOLUCION) / SUM(r.CANTIDAD_RECLAMOS) tiempo_promedio_resolucion, r.RECLAMO_TIPO_ID, r.RANGO_ETARIO_ID, r.MES, r.ANIO
   FROM BLACKSUITS.BI_H_RECLAMO AS r
   GROUP BY r.RECLAMO_TIPO_ID, r.RANGO_ETARIO_ID, r.MES, r.ANIO
GO

/*
Monto mensual generado en cupones a partir de reclamos.
*/

CREATE VIEW BLACKSUITS.monto_mensual_cupones_reclamos
AS
    SELECT SUM(r.TOTAL_CUPONES) monto_mensual_cupones, r.MES, r.ANIO
    FROM BLACKSUITS.BI_H_RECLAMO AS r
    GROUP BY r.MES, r.ANIO
    -- Por ahora no devuelve nada, porque no hay ningun cupon asociado con reclamos
	-- Esto lo vemos al buscar en la tabla maestra alguna fila con RECLAMO_NRO y CUPON_NRO no nulos, que no existen
GO
